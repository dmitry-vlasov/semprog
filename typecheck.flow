import term;

export {
    typecheck(term : AlgTerm, vars : Tree<string, AlgType>, signs : Tree<string, [AlgTypeFunc]>) -> AlgTerm;
}

typecheck(term : AlgTerm, vars : Tree<string, AlgType>, signs : Tree<string, [AlgTypeFunc]>) -> AlgTerm {
    //println("(A) typechecking: " + toString(term) + " ...");
    //println("(B) typechecking: " + algTerm2s(term) + " ...");
    ret = switch (term) {
        AlgTermVarUse(name, type): {
			resolved = lookupTreeDef(vars, name, type);
			if (resolved == undefAlgType) {
				println("UNDEF RESOLVED TYPE FOR: " + algTerm2s(term));
			}
            AlgTermVarUse(term with type = resolved);
        }
        AlgTermVarDecl(name, type): {
			if (type == undefAlgType) {
				println("UNDEF DECLARED TYPE FOR: " + algTerm2s(term));
			}
            term;
        }
        AlgTermFunc(name, args, type): {
            typed_args = fold(args, Pair([], vars), \acc, arg -> {
                typed = typecheck(arg, acc.second, signs);
                Pair(
                    concat(acc.first, [typed]),
                    switch (arg) {
                        AlgTermVarDecl(nm, tp): setTree(acc.second, nm, typed.type);
                        default: acc.second;
                    }
                );
            })
			arg_types = map(typed_args.first, \arg -> arg.type);
			find_val_type = \ops -> {
				fits = filtermap(ops, \op -> 
					maybeMap(
						matchTypes(funcArgTypes(op), arg_types, makeTree()),
						\sub -> Pair(op, sub)
					)
				);
				if (length(fits) == 0) {
					println("(B) no operator " + name + " matches argument types: " + strGlue(map(arg_types, \t -> algType2s(t)), ", "));
					println("ops: " + strGlue(map(ops, \op -> algType2s(op)), ", "));
					println("term: " + algTerm2s(term));
					undefAlgType;
				} else if (length(fits) > 1) {
					println("ambigous operator " + name + " matching with argument types: " + strGlue(map(arg_types, \t -> algType2s(t)), ", "));
					undefAlgType;
				} else {
					op = fits[0].first;
					sub = fits[0].second;
					if (op.name != "fn") {
						println("NOT fn: " + op.name);
						undefAlgType 
					} else {
						ret = instantiateType(funcValType(op), sub);
						if (ret == undefAlgType) {
							println("NOT typechecked: instantiateType(funcValType(op), sub) failed\n\t" + algTerm2s(term));
						}
						ret;
					}
				}
			}
			surrender = \msg-> {
				if (type == undefAlgType) {
					println(msg + "unknown oprator: " + name);
				}
				type
			}
            val_type = {
                if (exists(arg_types, \t -> t == undefAlgType)) {
                    println(
						"untyped: " + name + ": " + strGlue(map(arg_types, algType2s), ", ") + "\n" +
						"\t" + algTerm2s(term)
					);
                    undefAlgType;
                } else {
					switch (lookupTree(signs, name))  {
						Some(ops): find_val_type(ops);
						None(): {
							switch (lookupTree(typed_args.second, name)) {
								Some(var_type): {
									switch (var_type) {
										AlgTypeFunc(__,__): find_val_type([var_type]);
										default: surrender("A_");
									}
								} 
								None(): surrender("B_");
							}
						}
					};
				}
			}
            AlgTermFunc(term with 
                type = val_type,
                args = typed_args.first
            );
        }
    }
	if (ret.type == undefAlgType) {
    	println("NOT typechecked: " + algTerm2s(term) + " : " + algType2s(ret.type));
	}
    ret
}
