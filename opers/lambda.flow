import shower;
import dynamic;

export {
	lambdaOpers : [EvalOper];
}

lambdaOpers = [
	EvalOper(
        "lambda0", 
        funcAlgType([AlgTypeVar("V")], funcAlgType([], AlgTypeVar("V"))),
        \args -> Eval(\vals, conf -> {
            flow(\-> args[0].eval(vals, conf));
        }),
        \args -> Show(\conf -> {
			long = "lambda0 " + args[0].show(conf);
			if (strlen(long) <= conf.foldLen) long else {
				"lambda0\n" + strIndent(args[0].show(conf));
			}
		})
    ),
	EvalOper(
        "lambda1", 
        funcAlgType([AlgTypeVar("A"), AlgTypeVar("V")], funcAlgType([AlgTypeVar("A")], AlgTypeVar("V"))),
        \args -> Eval(\vals, conf -> {
            v = flow2s(args[0].eval(vals, conf));
            flow(\x -> args[1].eval(setTree(vals, v, x), conf));
        }),
        \args -> Show(\conf -> {
            long = "lambda1 " + args[0].show(conf) + " " + args[1].show(conf);
			if (strlen(long) <= conf.foldLen) long else {
				"lambda1 " + args[0].show(conf) + "\n" + strIndent(args[1].show(conf));
			}
		})
    ),
	EvalOper(
        "lambda2", 
        funcAlgType(
			[AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("V")], 
			funcAlgType([AlgTypeVar("A1"), AlgTypeVar("A2")], AlgTypeVar("V"))
		),
        \args -> Eval(\vals, conf -> {
            v1 = flow2s(args[0].eval(vals, conf));
			v2 = flow2s(args[1].eval(vals, conf));
            flow(\x, y -> args[2].eval(setTree(setTree(vals, v1, x), v2, y), conf));
        }),
        \args -> Show(\conf -> {
            long = "lambda2 " + args[0].show(conf) + ", " + args[1].show(conf) + " " + args[2].show(conf);
			if (strlen(long) <= conf.foldLen) long else {
				"lambda2 " + args[0].show(conf) + ", " + args[1].show(conf) + "\n" + strIndent(args[2].show(conf));
			}
		})
    ),
	EvalOper(
        "lambda3", 
        funcAlgType(
			[AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("A3"), AlgTypeVar("V")], 
			funcAlgType([AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("A3")], AlgTypeVar("V"))
		),
        \args -> Eval(\vals, conf -> {
            v1 = flow2s(args[0].eval(vals, conf));
			v2 = flow2s(args[1].eval(vals, conf));
			v3 = flow2s(args[2].eval(vals, conf));
            flow(\x, y, z -> args[3].eval(setTree(setTree(setTree(vals, v1, x), v2, y), v3, z), conf));
        }),
        \args -> Show(\conf -> {
            long = "lambda3 " + args[0].show(conf) + ", " + args[1].show(conf) + ", " + args[2].show(conf) + " " + args[3].show(conf);
			if (strlen(long) <= conf.foldLen) long else {
				"lambda3 " + args[0].show(conf) + ", " + args[1].show(conf) + ", " + args[2].show(conf) + "\n" + strIndent(args[3].show(conf));
			}
		})
    ),
	EvalOper(
        "call0", 
        funcAlgType(
			[funcAlgType([], AlgTypeVar("V"))], 
			AlgTypeVar("V")
		),
        \args -> Eval(\vals, conf -> {
            fn = cast(args[0].eval(vals, conf) : flow -> () -> flow);
            fn();
        }),
        \args -> Show(\conf -> {
			long = "call0(" + args[0].show(conf) + ")";
			if (strlen(long) <= conf.foldLen) long else {
				"call0(\n" + strIndent(args[0].show(conf)) + "\n)";
			}
		})
    ),
    EvalOper(
        "call1", 
        funcAlgType(
			[
				funcAlgType([AlgTypeVar("A")], AlgTypeVar("V")), 
				AlgTypeVar("A")
			], 
			AlgTypeVar("V")
		),
        \args -> Eval(\vals, conf -> {
            fn = cast(args[0].eval(vals, conf) : flow -> (flow) -> flow);
            ar = args[1].eval(vals, conf);
            fn(ar);
        }),
        \args -> Show(\conf -> {
            long = "call1(" + args[0].show(conf) + ", " + args[1].show(conf) + ")";
			if (strlen(long) <= conf.foldLen) long else {
				"call1(\n" + 
					strIndent(args[0].show(conf)) + ",\n" + 
					strIndent(args[1].show(conf)) + 
				"\n)";
			}
		})
    ),
	EvalOper(
        "call2", 
        funcAlgType(
			[
				funcAlgType([AlgTypeVar("A1"), AlgTypeVar("A2")], AlgTypeVar("V")), 
				AlgTypeVar("A1"), AlgTypeVar("A2")
			], 
			AlgTypeVar("V")
		),
        \args -> Eval(\vals, conf -> {
            fn = cast(args[0].eval(vals, conf) : flow -> (flow, flow) -> flow);
            a_1 = args[1].eval(vals, conf);
			a_2 = args[2].eval(vals, conf);
            fn(a_1, a_2);
        }),
        \args -> Show(\conf -> {
            long = "call2(" + args[0].show(conf) + ", " + args[1].show(conf) + ", " + args[2].show(conf) + ")";
			if (strlen(long) <= conf.foldLen) long else {
				"call2(\n" + 
					strIndent(args[0].show(conf)) + ",\n" + 
					strIndent(args[1].show(conf)) + ",\n" + 
					strIndent(args[2].show(conf)) + 
				"\n)";
			}
		})
    ),
	EvalOper(
        "call3", 
        funcAlgType(
			[
				funcAlgType([AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("A3")], AlgTypeVar("V")), 
				AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("A3")
			], 
			AlgTypeVar("V")
		),
        \args -> Eval(\vals, conf -> {
            fn = cast(args[0].eval(vals, conf) : flow -> (flow, flow, flow) -> flow);
            a_1 = args[1].eval(vals, conf);
			a_2 = args[2].eval(vals, conf);
			a_3 = args[3].eval(vals, conf);
            fn(a_1, a_2, a_3);
        }),
        \args -> Show(\conf -> {
            long = "call3(" + args[0].show(conf) + ", " + args[1].show(conf) + ", " + args[2].show(conf) + ", " + args[3].show(conf) + ")";
			if (strlen(long) <= conf.foldLen) long else {
				"call3(\n" + 
					strIndent(args[0].show(conf)) + ",\n" + 
					strIndent(args[1].show(conf)) + ",\n" + 
					strIndent(args[2].show(conf)) + ",\n" + 
					strIndent(args[3].show(conf)) + 
				"\n)";
			}
		})
    ),
	EvalOper(
        "rec1", 
        funcAlgType(
			[funcAlgType([AlgTypeVar("A1"), AlgTypeVar("A2"), AlgTypeVar("A3")], AlgTypeVar("V"))], 
			funcAlgType([AlgTypeVar("A1")], AlgTypeVar("V"))
		),
        \args -> Eval(\vals, conf -> {
            fn = args[0].eval(vals, conf);
			flow(\n -> 
				cast(fn : flow -> ((flow, flow, flow) -> flow))(n, 1, fn)
			);
        }),
        \args -> Show(\conf -> {
            long = "rec1(" + args[0].show(conf) + ")";
			if (strlen(long) <= conf.foldLen) long else {
				"rec1(\n" + strIndent(args[0].show(conf)) + "\n)";
			}
		})
    ),
];

doRecursion(
    f_0 : ([flow]) -> flow,
    f_1 : ([flow]) -> flow,
    g : ([flow]) -> flow,
    p : ([flow]) -> flow,
    args : [flow]
) -> flow {
    if (flow2b(p(args))) f_0(args) else {
        f_1(concat(
            [doRecursion(f_0, f_1, g, p, 
                concat([g(args)], tail(args))
            )],
            args
        ));
    }
}

recursion(
    f_0 : ([flow]) -> flow,
    f_1 : ([flow]) -> flow,
    g : ([flow]) -> flow,
    p : ([flow]) -> flow
) -> ([flow]) -> flow {
    \args -> doRecursion(f_0, f_1, g, p, args);
}

makeRecursion(n : AlgType, arg_types : [AlgType], val : AlgType) -> EvalOper {
    EvalOper("rec", 
        funcAlgType(
            [
                funcAlgType(concat([n], arg_types), val),       // f_0
                funcAlgType(concat([val, n], arg_types), val),  // f_1
                funcAlgType(concat([n], arg_types), n),         // g
                funcAlgType(concat([n], arg_types), boolType),  // p
            ],
            funcAlgType(concat([n], arg_types), val)
        ),
        \args -> Eval(\vals, conf -> {
            f_0 = cast(args[0].eval(vals, conf): flow -> ([flow]) -> flow);
            f_1 = cast(args[1].eval(vals, conf): flow -> ([flow]) -> flow);
            g   = cast(args[2].eval(vals, conf): flow -> ([flow]) -> flow);
            p   = cast(args[3].eval(vals, conf): flow -> ([flow]) -> flow);
            recursion(f_0, f_1, g, p);
        }),
        showDfEvalOper("rec")
    );
}

