import shower;
import dynamic;
import common_types;
import math/math;

export {
    // Integer operations
    makeIntConst(name : string, val : int) -> EvalOper;
    makeIntUnary(name : string, op : (int) -> int) -> EvalOper;
    makeIntBinary(name : string, op : (int, int) -> int) -> EvalOper;

    intOpers : [EvalOper];
    intConst : EvalConst;
}

intOpers = [
    makeIntUnary("-", \x -> -x),
    EvalOper("+",
        funcAlgType([variadicAlgType(intType)], intType),
        \args -> Eval(\vals, conf -> {
            arg_vals = evalArgs(args, vals, conf);
            Val(fold(
                map(arg_vals, \arg -> flow2i(arg.val)), 0, \acc, i -> acc + i
            ), intType);
        }),
        showInfixVariadicEvalOper("+")
    ),
    EvalOper("*",
        funcAlgType([variadicAlgType(intType)], intType),
        \args -> Eval(\vals, conf -> {
            arg_vals = evalArgs(args, vals, conf);
            Val(fold(
                map(arg_vals, \arg -> flow2i(arg.val)), 1, \acc, i -> acc * i
            ), intType);
        }),
        showInfixVariadicEvalOper("*")
    ),
    makeIntBinary("/", \x, y -> x / y),
    makeIntBinary("-", \x, y -> x - y)
];

intConst = EvalConst(
    intType, 
    \n, conf -> if (isDigits(n)) {
        Val(s2i(n), intType);
    } else {
        println("wrong integer format: " + n);
        Val(undefVal, intType);
    }
);

makeIntConst(name : string, val : int) -> EvalOper {
    EvalOper(name,
        funcAlgType([], intType),
        \args -> Eval(\__,conf -> Val(val, intType)),
        showConstEvalOper(name)
    )
}

makeIntUnary(name : string, op : (int) -> int) -> EvalOper {
    EvalOper(name, 
        funcAlgType([intType], intType),
        \args -> Eval(\vals, conf -> {
			arg_val = args[0].eval(vals, conf).val;
			if (arg_val == undefVal) Val(undefVal, intType) else {
				Val(op(flow2i(arg_val)), intType);
			}
		}),
        showPrefixUnaryEvalOper(name)
    )
}

makeIntBinary(name : string, op : (int, int) -> int) -> EvalOper {
    EvalOper(name, 
        funcAlgType([intType, intType], intType),
        \args -> Eval(\vals, conf -> {
			arg1_val = args[0].eval(vals, conf).val;
			if (arg1_val == undefVal) Val(undefVal, intType) else {
				arg2_val = args[1].eval(vals, conf).val;
				if (arg2_val == undefVal) Val(undefVal, intType) else {
					Val(
						op(flow2i(arg1_val), flow2i(arg2_val)), 
						intType
					);
				}
			}
        }),
        showInfixBinaryEvalOper(name)
    );
}
