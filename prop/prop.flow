import eval;
import dynamic;

export {
    // The definition of semantics for a classical propositional logic.
    makeBoolConst(name : string, v : bool) -> EvalOper;
    boolTrue = makeBoolConst("T", true);
    boolFalse = makeBoolConst("F", false);

    makeBoolUnary(name : string, op : (bool) -> bool) -> EvalOper;
    lnot = makeBoolUnary("!", \v -> !v);

    makeBoolBinary(name : string, op : (bool, bool) -> bool) -> EvalOper;
    lor = makeBoolBinary("\\/", \v1, v2 -> v1 || v2);
    land = makeBoolBinary("/\\", \v1, v2 -> v1 && v2);
    limpl = makeBoolBinary("->", \v1, v2 -> !v1 || v2);
    leq = makeBoolBinary("<->", \v1, v2 -> (v1 && v2) || (!v1 && !v2));
    
    propLogic = EvalAlgebra("propositioal logic", [boolTrue, boolFalse, lnot, land, limpl, leq, lor]);

    makeBoolQuantifier(BQ : string, v : string) -> EvalOper;
    makeQBFAlgebra(vars : [string]) -> EvalAlgebra;

    getBoolQuantifiers(f : AlgTerm) -> [string];
}


makeQBFAlgebra(vars : [string]) -> EvalAlgebra {
    EvalAlgebra("QBF logic", concat3(
        propLogic.opers, 
        map(vars, \v -> makeBoolQuantifier("BA", v)), 
        map(vars, \v -> makeBoolQuantifier("BE", v))  
        )
    );
}

makeBoolQuantifier(BQ : string, v : string) -> EvalOper {
    EvalOper(BQ + " " + v,
        fnType([boolType] , boolType),
        \e, args, vals -> {
            if (length(args) != 2) {
                flow(undefVal)
            } else {
                if (BQ == "BA") {
                    flow( 
                        flow2b(e.eval(args[1], setTree(vals, v, true))) 
                        && 
                        flow2b(e.eval(args[1], setTree(vals, v, false)))
                        );
                } else {
                    flow( 
                        flow2b(e.eval(args[1], setTree(vals, v, true))) 
                        || 
                        flow2b(e.eval(args[1], setTree(vals, v, false)))
                        );
                }
            }
        },
        \s, args -> {
            if (length(args) != 2) "broken term" else
            BQ + " " + v + " " + s.show(args[1]);
        }
    ); 
}

makeBoolConst(name : string, v : bool) -> EvalOper {
    EvalOper(name, boolType, 
        \__, args, __ -> {
            if (length(args) != 0) flow(undefVal) else flow(v)
        },
        \__,__ -> name
    );
}

makeBoolUnary(name : string, op : (bool) -> bool) -> EvalOper {
    EvalOper(name, unaryBoolFnType, 
        \e, args, vals -> {
            if (length(args) != 1) flow(undefVal) else {
                flow(op(flow2b(e.eval(args[0], vals))));
            }
        },
        showUnaryEvalOper(name)
    )
}

makeBoolBinary(name : string, op : (bool, bool) -> bool) -> EvalOper {
    EvalOper(name, binaryBoolFnType,
        \e, args, vals -> {
            if (length(args) != 2) flow(undefVal) else {
                flow(op(
                    flow2b(e.eval(args[0], vals)), 
                    flow2b(e.eval(args[1], vals))
                ));
            }
        },
        showBinaryEvalOper(name)
    );
}

getBoolQuantifiers(f : AlgTerm) -> [string] {
    vs = doGetBoolQuantifiers(makeSet(), f);
    set2array (mapSet(vs, \v -> v.name))
}

doGetBoolQuantifiers(acc : Set<AlgTermVar>, f : AlgTerm) -> Set<AlgTermVar> {
   switch (f) {
        AlgTermVar(var, type): acc;
        AlgTermFunc(op, args, type): 
            if (!(startsWith(op, "BA ") || startsWith(op, "BE "))) {
                fold(args, acc, doGetBoolQuantifiers);
            } else {
                if (length(args) == 0) acc else {
                    v = args[0];
                    switch (v) {
                        AlgTermVar(__,__): 
                            fold(tail(args), insertSet(acc, v), doGetBoolQuantifiers);
                        AlgTermFunc(__,__,__): 
                            fold(args, acc, doGetBoolQuantifiers);
                    }
                }
            }
   } 
}