import shower;
import typecheck;

export {
    Module(name : string, defs : [Def]);

    Def(
        name : string,
        vars : [AlgTermVarDecl],
        type : AlgType,
        term : AlgTerm
    );

    makeDefOper(def : Def) -> EvalOper;
    module2s(module : Module, conf : Conf) -> string;
    def2s(def : Def, conf : Conf) -> string;
    typecheckModule(module : Module, algebra : EvalAlgebra) -> Module;
}

makeDefOper(def : Def) -> EvalOper {
    EvalOper(
        def.name,
        funcAlgType(map(def.vars, \v -> v.type), def.term.type),
        \args -> Eval(\vals, conf -> 
            if (length(args) != length(def.vars)) {
                println("Function " + def.name + " arity " + i2s(length(def.vars)) + " mismatch arguments length " + i2s(length(args)));
                flow(undefVal)
            } else {
                arg_vals = evalArgs(args, vals, conf);
                if (conf.trace) {
                     println(strIndentMany("calling func " + def.name + " with args " + toString(arg_vals), ^(conf.callDepth)));
                }
                vals1 = foldi(def.vars, vals, \i, acc, var -> 
                    setTree(acc, var.name, arg_vals[i])
                );
                ^semantics(def.term).eval(vals1, conf);
            }
        ),
        showPrefixEvalOper(def.name)
    );
}

module2s(module : Module, conf : Conf) -> string {
    strGlue(map(module.defs, \def -> def2s(def, conf)), "\n");
}

def2s(def : Def, conf : Conf) -> string {
    "def " +  def.name + 
    " (" + strGlue(map(def.vars, algTerm2s), ", ") + ")" + 
    " : " + algType2s(def.type) + " {\n" +
     strIndent(^shower(def.term).show(conf)) + "\n}\n";
}

/*typedVar2s(var : AlgTermVarDecl) -> string {
    var.name + " : " + algType2s(var.type);
}*/

typecheckModule(module : Module, algebra : EvalAlgebra) -> Module {
    alg_signs = fold(algebra.opers, makeTree(), \acc, oper ->
        setTree(acc, oper.name, concat(lookupTreeDef(acc, oper.name, []), [oper.sign]))
    );
    signs = fold(module.defs, alg_signs, \acc, def ->
        setTree(acc, def.name, concat(lookupTreeDef(acc, def.name, []), [defSign(def)]))
    );
    Module(module with 
        defs = map(module.defs, \def -> {
            //println("typechecking: " + def.name);
            vars = fold(def.vars, makeTree(), \acc, var -> setTree(acc, var.name, var.type));
            typechecked = typecheck(def.term, vars, signs);
            if (typechecked.type == undefAlgType) {
                println("Term " + algTerm2s(def.term) + " was not typed");
            }
            Def(def with term = typechecked);
        })
    );
}

defSign(def : Def) -> AlgTypeFunc {
    funcAlgType(map(def.vars, \v -> v.type), def.type);
}